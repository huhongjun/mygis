<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
// Copyright Â© 2008 ESRI
//
// All rights reserved under the copyright laws of the United States.
// You may freely redistribute and use this software, with or
// without modification, provided you include the original copyright
// and use restrictions.  See use restrictions in the file:
// <install location>/FlexViewer/License.txt
//
////////////////////////////////////////////////////////////////////////////////
-->
<mx:VBox	xmlns:mx		="http://www.adobe.com/2006/mxml" 
			styleName		="MenuBox" 
			width			="100%" 
			verticalGap		="0" 
			horizontalAlign	="center">
	
	<mx:Script>
		<![CDATA[
		
			import com.esri.solutions.flexviewer.AppEvent;
			import com.esri.solutions.flexviewer.SiteContainer;
			
			private const STYLE_MENU_ACTIVE:String = "MenuBoxActive";
			
			private const STYLE_MENU_NORMAL:String = "MenuBox";
			
			/**
			 * itemId is the runtime assigned numeric number to the menu item.
			 */
			public var itemId:Number;
			 
			/**
			 * itemIcon allows add a small icon to the left of the menu item to give
			 * a visual indication of the funtion of the menu item.
			 */
			[Bindable]
			public var itemIcon:String;
			
			/**
			 * itemLabel is the text label of the menu item.
			 */
			[Bindable]
			public var itemLabel:String;
			
			/**
			 * itemRuleVisible toggles the speration line between menu items. 
			 * It can be set to be visible or invisible and is automatically 
			 * set to invisible for the last menu item.
			 * 
			 * @default true
			 */
			[Bindable]
			public var itemRuleVisible:Boolean = true;
						
			public var itemAction:String;
						
			public var itemValue:String;

            /**
            * @private
            * TODO: logic here need to be refactoried to remove the depandency on named menus.
            */						
			private function menuItemClick():void
			{
				switch (itemAction)
				{
					case "widget":
					{
						SiteContainer.dispatchEvent(new AppEvent(AppEvent.WIDGET_MENU_CLICKED, false, false, itemId));
						break;
					}
						
					case "navtool":
					{
						var data:Object =
						{
							tool: itemValue,
							status: itemLabel
						}
						SiteContainer.dispatchEvent(new AppEvent(AppEvent.SET_MAP_NAVIGATION, false, false, data));
						break;
					}
						
					case "basemap":
					{
						SiteContainer.dispatchEvent(new AppEvent(AppEvent.BASEMAP_MENU_CLICKED, false, false, itemValue));
						break;
					}
						
					case "link":
					{
						navigateToURL(new URLRequest(itemValue));
						break;
					}
						
					case "function":
					{
						break;
					}
				}
			}
			
			private function highlight():void
			{
				this.styleName = this.STYLE_MENU_ACTIVE;
			}
			
			private function clearHighlight():void
			{
				this.styleName = this.STYLE_MENU_NORMAL;
			}
					
		]]>
	</mx:Script>
	
	<mx:HBox horizontalGap="0" verticalAlign="middle" horizontalAlign="left"  mouseOver="highlight()" mouseOut="clearHighlight()" mouseChildren="false" click="menuItemClick()" width="100%" buttonMode="true" useHandCursor="true" height="24">
		<mx:Spacer width="10"/>
		<mx:Image source="{itemIcon}" height="20" width="20"/>
		<mx:Label text="{itemLabel}" styleName="MenuItem" paddingLeft="2" />
	</mx:HBox> 
	<mx:HRule styleName="ControllerBreak" alpha="0.3" visible="{itemRuleVisible}" width="90%" />	

</mx:VBox>
